
; 色設定を緑にしてUAVの電波の強さ描画を呼び出す。色設定はCall先にも引き継がれる。
Color = 0xFF28d448
Call = uav_fs

; インベントリ数を表示
If = inventory > 0
	Color = 0xCF1FFF1F
	DrawString = 120, -80, "Inventory [ %d ]", INVENTORY
Endif

; ヨーの目盛描画
Color = 0xCFFFFFFF
DrawGraduationYaw   = plyr_yaw, 0,   0, -100
DrawLine = 0,-87,   2,-83,  -2,-83,   0,-87
DrawCenteredString =   0, -81, "%.0f", PLYR_YAW

; ピッチの目盛描画
DrawGraduationPitch1 = plyr_pitch, 0, 0, 0
DrawCenteredString = -140, -4, "%.0f", PLYR_PITCH
DrawLine = -120,0,   -120-5,-5,   -120-30,-5,   -120-30,5,   -120-5,5,   -120,0  
DrawCenteredString = +140, -4, "%.0f", PLYR_PITCH
DrawLine =  120,0,    120+5,-5,    120+30,-5,    120+30,5,    120+5,5,    120,0


; スロットル(Throttle)
DrawTexture = plane_hud_wwii, -170,   83, 32,32,   0,192, 64,64
DrawTexture = plane_hud_wwii, -170+8, 83, 16,32,  96,192, 32,64, Throttle*270-180


; 燃料(Fuel)
if = time<=13000
	; 昼用の計器
	DrawTexture = bnr32_hud, -207,   83, 32,32,  00,128, 64,64
	DrawTexture = bnr32_hud, -193,   81, 16,48,  64,160, 32,96, fuel*65-83
Endif

if = time> 13000
	; 夜間用の計器
	DrawTexture = bnr32_hud, -207,   83, 32,32,  00,192, 64,64
	DrawTexture = bnr32_hud, -193,   81, 16,48,  96,160, 32,96, fuel*65-83
Endif


; 姿勢指示器
DrawTexture = plane_hud_wwii, 195, 37,  42,42,  142,64+(40*(pitch/90)), 100,128, -roll
DrawTexture = plane_hud_wwii, 184, 26,  64,64,    0,0,                  128,128

; 高度計
if = time<=13000
	; 昼用の計器
	DrawTexture = plane_hud_wwii, 120, 26,  64,64,  256,  0, 128,128
Endif
if = time> 13000
	; 夜間用の計器
	DrawTexture = plane_hud_wwii, 120, 26,  64,64,  256,128, 128,128
Endif
DrawTexture = plane_hud_wwii,    144, 26,  16,64,  416,0,  32,128, sea_alt*0.036
DrawTexture = plane_hud_wwii,    144, 26,  16,64,  384,0,  32,128, sea_alt*0.36


; 時計(time)
; マイクラ内の時間と分を表示
DrawString = -205, 60, "Time:   %02d:%02d", MC_THOR, MC_TMIN

; 弾数, 残弾 (Ammo) リロード中でなければ緑、リロード中であれば赤
Color = 0xFFFFE400
DrawLine =   70,  67,   -70,  67
Color = reloading==0? 0xFF28d448: 0xFFDF0408
;DrawCenteredString =   0, 45, "%.2f%% %.2fsec",   RELOAD_PER, RELOAD_SEC
DrawCenteredString = -26, 68, "%s",  WPN_AMMO
DrawCenteredString =   0, 68, "/",   WPN_AMMO
DrawCenteredString =  26, 68, "%s",  WPN_RM_AMMO
; 武器名 (Weapon name)
DrawCenteredString = 0, 57, "%s",  WPN_NAME

; 銃身の熱量タイプの場合(Heat)
If = is_heat_wpn==1
	Color = 0xCFA99D38
	DrawRect = -20, 77, 41, 2
	Color = 0xFFDF0408
	DrawRect = -20, 77, 41*wpn_heat, 2
EndIf

;リロード時間　リロード中でなければ緑、リロード中であれば赤
Color = reload_time<0.01? 0xFF28d448: 0xFFDF0408
DrawLine =     -30, 87,   32, 87,    32, 79,    -30, 79,   -30,  87
Color = reload_time<0.01? 0xFFFFE400: 0xFFDF0408
DrawString  = 40,  79, "%3.1f", RELOAD_SEC
DrawString  = 40+10,  79, "sec", RELOAD_SEC
DrawString  = -30-45, 79, "Reload Time"
DrawRect    = -30,  80,   reload_time*60, 6






; 色設定(Current Color)
Color = 0xFF28d448
; 座標(Position)
DrawString = -205, 20, "X: %+.1f", pos_X
DrawString = -205, 30, "Y: %+.1f", pos_Y
DrawString = -205, 40, "Z: %+.1f", pos_Z
DrawString = -150, 20, "[%+.2f]",  motion_X
DrawString = -150, 30, "[%+.2f]",  motion_Y
DrawString = -150, 40, "[%+.2f]",  motion_Z
DrawString = -205, 20, "X: %+.1f", pos_X
DrawString = -205, 30, "Y: %+.1f", pos_Y
DrawString = -205, 40, "Z: %+.1f", pos_Z
DrawString = -150, 20, "[%+.2f]",  motion_X
DrawString = -150, 30, "[%+.2f]",  motion_Y
DrawString = -150, 40, "[%+.2f]",  motion_Z



; 燃料の低下を警告表示
If = low_fuel
	DrawTexture = hud, 40,-85, 16,16,  0,0,  32,32
	Color = 0xFFDF0408
	DrawString  = 56,-80, "%s key", KEY_GUI
EndIf

; HP
Color = hp_rto>0.2? 0xFF28d448: 0xFFDF0408
DrawLine =  145, 22,   207, 22,    207, 14,    145, 14,   145,  22,
Color = hp_rto>0.2? 0xFFFFE400: 0xFFDF0408
DrawString  = 150,     5, "HP"
DrawString  = 160,     5, "%3.0f", HP_PER
DrawString  = 160+26,  5, "/ 100", HP_PER
DrawRect    = 146,    15,   hp_rto*60, 6



Color = 0xFFFFE400
If = free_look==1
	DrawString = 145, -40, "FREE LOOK"
Endif

If = cam_mode==1
	DrawString = 145, -30, "NIGHT VISION"
Endif
If = cam_mode==2
	DrawString = 145, -30, "THERMAL VISION"
Endif

If = have_flare==1
	Color = can_flare==1? 0xFFFFE400: 0xFFDF0408
	DrawCenteredString = 60, 45, "[ FLARE ]"
Endif

; レーダーを持つ機体の場合
If = have_radar==1
	DrawTexture = plane_hud, 144, 37,  64,64,  0,0, 128,128

	Color = 0xFF28d448
	DrawEntityRadar = -plyr_yaw-180, 144, 37,  64,64

	Color = 0xFFDF0408
	DrawEnemyRadar  = -plyr_yaw-180, 144, 37,  64,64

	DrawTexture = plane_hud, 144+16, 37,  32,64,  128,0, 64,128, radar_rot
EndIf

